plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

import org.gradle.api.GradleException
import java.util.Locale

group 'com.surrealdb'
version '3.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    integrationTest {
        java
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext.nativePlatformDir = {
    final String vendor = (System.getProperty('java.vendor') ?: '').toLowerCase(Locale.ENGLISH)
    final String arch = (System.getProperty('os.arch') ?: '').toLowerCase(Locale.ENGLISH)
    final String name = (System.getProperty('os.name') ?: '').toLowerCase(Locale.ENGLISH)
    final boolean android = vendor.contains('android')
    final boolean linux = !android && name.contains('linux')
    final boolean windows = !linux && name.contains('win')
    final boolean osx = !windows && name.contains('mac')
    final boolean intel64 = arch.contains('x86_64') || arch.contains('amd64')
    final boolean intel32 = (!intel64 && arch.contains('i386')) || arch.contains('i686') || arch.contains('x86')
    final boolean arm64 = arch.contains('aarch64') || arch.contains('arm64')
    final boolean arm32 = !arm64 && (arch.contains('armv7') || arch.contains('arm'))

    if (android) {
        if (arm64) return 'android_arm64'
        if (intel64) return 'android_64'
        if (arm32) return 'android_arm32'
        if (intel32) return 'android_32'
    } else if (linux) {
        if (arm64) return 'linux_arm64'
        if (intel64) return 'linux_64'
        if (arm32) return 'linux_arm32'
        if (intel32) return 'linux_32'
    } else if (windows) {
        if (intel64) return 'windows_64'
        if (intel32) return 'windows_32'
    } else if (osx) {
        if (arm64) return 'osx_arm64'
        if (intel64) return 'osx_64'
    }
    throw new GradleException("Unsupported architecture: ${arch} - name: ${name}")
}

ext.nativeLibName = System.mapLibraryName('surrealdb')
ext.prebuiltNativeJar = file("native/surrealdb-${version}.jar")

ext.isGithubActions = (System.getenv('GITHUB_ACTIONS') ?: 'false').toBoolean()
// Local dev: embed native by default. CI: do not auto-embed (cross.yml assembles natives).
ext.autoEmbedNative = !isGithubActions

tasks.named('jar') {
    doLast {
        def platform = nativePlatformDir()
        def expectedEntry = "natives/${platform}/${nativeLibName}".toString()
        def jarFile = archiveFile.get().asFile

        // If we're not auto-embedding (e.g. GitHub Actions) and there is no prepackaged
        // native for this platform, don't fail the build here.
        def prepackagedNative = file("src/main/resources/${expectedEntry}")
        if (!autoEmbedNative && !prepackagedNative.exists()) {
            return
        }

        def zip = new java.util.zip.ZipFile(jarFile)
        try {
            if (zip.getEntry(expectedEntry) == null) {
                throw new GradleException("Missing native library in JAR: ${expectedEntry} (jar: ${jarFile})")
            }
        } finally {
            zip.close()
        }
    }
}


dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    // classes produced by src/main/java
    integrationTestImplementation sourceSets.main.output
    if (prebuiltNativeJar.exists()) {
        // If CI (or a user) provides a prebuilt, multi-platform JAR in native/, test against it.
        integrationTestImplementation files(prebuiltNativeJar)
    }
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    systemProperty 'java.library.path', file('target/debug').absolutePath
    testLogging {
        events "passed"
    }
}

jacocoTestReport {
    dependsOn test
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

tasks.register('createCombinedReport') {
    dependsOn jacocoTestReport
    dependsOn javadoc

    doLast {
        // Copy the javadoc
        def javadocSource = file("build/docs/javadoc")
        def javadocDestination = file("build/reports/javadoc")
        // Ensure destination exists and copy
        delete javadocDestination  // Clean old docs if present
        mkdir javadocDestination
        copy {
            from javadocSource
            into javadocDestination
        }

        def indexFile = file("build/reports/index.html")
        indexFile.text = """
            <!DOCTYPE html>
            <html>
            <head>
                <title>Combined Test, Coverage, and Javadoc Report</title>
            </head>
            <body>
                <h1>Combined Test, Coverage, and Javadoc Report</h1>
                <ul>
                    <li><a href="./tests/test/index.html">Test Report</a></li>
                    <li><a href="./jacoco/test/html/index.html">JaCoCo Coverage Report</a></li>
                    <li><a href="./javadoc/index.html">Javadoc</a></li>
                </ul>
            </body>
            </html>
        """
    }
}


tasks.register('integrationTest', Test) {
    if (autoEmbedNative && !prebuiltNativeJar.exists()) {
        dependsOn 'cargoBuild'
    }
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'java.library.path', file('target/debug').absolutePath
    // use target/release for a release build
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = 'full'
        showCauses = true
        showStackTraces = true
    }
}

tasks.register('cargoBuild', Exec) {
    description = 'Builds the native Rust library for local tests.'
    group = 'build'
    commandLine 'cargo', 'build'
}

tasks.named('processResources') {
    def platform = nativePlatformDir()
    def prepackagedNative = file("src/main/resources/natives/${platform}/${nativeLibName}")
    if (autoEmbedNative && !prepackagedNative.exists()) {
        dependsOn 'cargoBuild'
        from("${project.projectDir}/target/debug") {
            include nativeLibName
            into "natives/${platform}"
        }
    }
}

project.afterEvaluate {
    def key = System.getenv('SIGNING_KEY')
    def _pass = System.getenv('SIGNING_KEY_PASS')

    signing {
        useInMemoryPgpKeys(key, _pass)
        sign publishing.publications.mavenJava
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/surrealdb/surrealdb.java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "OSSRH"
            def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.rootProject.name
            version = project.version

            from components.java

            pom {
                name = 'SurrealDB Driver'
                packaging = 'jar'
                description = 'The driver for accessing a SurrealDB instance.'
                url = 'https://surrealdb.com/docs/integration/libraries/java'

                scm {
                    connection = 'scm:git:git@github.com:surrealdb/surrealdb.java.git'
                    url = 'https://github.com/surrealdb/surrealdb.java'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'tobiemh'
                        name = 'Tobie Morgan Hitchcock'
                        email = 'tobie@surrealdb.com'
                    }
                    developer {
                        id = 'phughk'
                        name = 'Hugh Kaznowski'
                        email = 'hugh@surrealdb.com'
                    }
                    developer {
                        id = 'emmanuel-keller'
                        name = 'Emmanuel Keller'
                        email = 'emmanuel.keller@surrealdb.com'
                    }
                }
            }
        }
    }
}